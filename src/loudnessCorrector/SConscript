Import( 'env' )
Import( 'loudnessAssessmentVersionStr' )
Import( 'loudnessAnalyserLibStatic' )

### loudness Corrector ###

loudnessCorrectorLibName = 'loudnessCorrector'
# add version number to the name of the shared library if Windows platform
if env['PLATFORM'] == 'win32':
    loudnessCorrectorLibName += '-' + loudnessAssessmentVersionStr

loudnessCorrectorSrc = Glob( '*.cpp' )

loudnessCorrectorDeps = [
    loudnessAnalyserLibStatic,
]

loudnessCorrectorLib = env.SharedLibrary(
    target = loudnessCorrectorLibName,
    source = loudnessCorrectorSrc,
    LIBS = loudnessCorrectorDeps
)

loudnessCorrectorLibStatic = env.StaticLibrary(
    target = 'loudnessCorrector',
    source = loudnessCorrectorSrc,
    LIBS = loudnessCorrectorDeps
)

# Install loudnessCorrector libs
env.Alias( 'install', env.InstallVersionedLib( 'lib', loudnessCorrectorLib ) )
env.Alias( 'install', env.Install( 'lib', loudnessCorrectorLibStatic ) )

Export( 'loudnessCorrectorLibStatic' )
