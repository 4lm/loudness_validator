Import( 'env' )
Import( 'loudnessAssessmentVersionStr' )
Import( 'loudnessAnalyserLibStatic' )

### loudness Corrector ###

loudnessCorrectorLibName = 'loudnessCorrector'
# add version number to the name of the shared library if Windows platform
if env['PLATFORM'] == 'win32':
    loudnessCorrectorLibName += '-' + loudnessAssessmentVersionStr
loudnessCorrectorLib = env.SharedLibrary(
    loudnessCorrectorLibName,
    Glob( '*.cpp' ),
    LIBS = [
        loudnessAnalyserLibStatic,
    ]
)

loudnessCorrectorLibStatic = env.StaticLibrary(
    'loudnessCorrector',
    Glob( '*.cpp' ),
    LIBS = [
        loudnessAnalyserLibStatic,
    ]
)

# Install loudnessCorrector libs
env.Alias( 'install', env.InstallVersionedLib( 'lib', loudnessCorrectorLib ) )
env.Alias( 'install', env.Install( 'lib', loudnessCorrectorLibStatic ) )

Export( 'loudnessCorrectorLibStatic' )
