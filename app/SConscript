import os
Import( 'env' )

Import( 'loudnessAnalyserLibStatic' )
Import( 'loudnessToolsLibStatic' )

loudnessAnalyserProgram = env.Program(
	'loudness-analyser',
	Glob( 'analyser/*.cpp' ),
	LIBS = [
		loudnessAnalyserLibStatic,
		loudnessToolsLibStatic,
		"sndfile",
	]
)

loudnessCorrectorProgram = env.Program(
	'loudness-corrector',
	Glob( 'corrector/*.cpp' ),
	LIBS = [
		loudnessAnalyserLibStatic,
		loudnessToolsLibStatic,
		"sndfile",
	]
)

env.Alias( 'install', env.Install( 'bin', loudnessAnalyserProgram ) )
env.Alias( 'install', env.Install( 'bin', loudnessCorrectorProgram ) )

# check qt4 library
conf = Configure(env)
if conf.CheckLib('QtCore', language='cxx'):
    env = conf.Finish()

    qtEnv = env.Clone()
    qtEnv.Tool('qt')

    loudnessValidatorLibraries = [
        loudnessAnalyserLibStatic,
        loudnessToolsLibStatic,
        "sndfile",
    ]

    loudnessValidatorFrameworks = []

    QtLibs = [
        "QtCore",
        "QtGui",
    ]

    if not qtEnv['PLATFORM'] == "darwin":
        loudnessValidatorLibraries.append( QtLibs )
    else:
        loudnessValidatorFrameworks.append( QtLibs )

    loudnessValidatorProgram = qtEnv.Program(
        'loudness-validator',
        Glob( 'LoudnessValidator/*.cpp' ),
        LIBS = loudnessValidatorLibraries,
        FRAMEWORKS = loudnessValidatorFrameworks,
    )

    qtEnv.Alias( 'install', qtEnv.Install( 'bin', loudnessValidatorProgram ) )

else:
    print 'Warning: did not find QtCore library, will not build loudness-validator app.'
